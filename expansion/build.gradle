plugins {
    id 'net.minecraftforge.gradle'
    id 'com.matthewprenger.cursegradle'
    id 'org.spongepowered.mixin'
}

String mod_id = 'projectred_expansion'

minecraft {
    mappings channel: mcp_mappings, version: mcp_mappings_version
    accessTransformer = file("../core/src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        data {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            ideaModule "${rootProject.name}.${project.name}.main"

            workingDirectory file('run')
            args '--mod', mod_id, '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods {
                'projectred_core' { source project(':core').sourceSets.main }
                '${mod_id}' { source sourceSets.main }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal")
    implementation fg.deobf("codechicken:CBMultipart:${mc_version}-${cbm_version}:universal")

    implementation project(":core")
}

mixin {
    // This will automatically add manifest entry to jar
    add sourceSets.main, 'mixins.projectred.expansion.refmap.json'
    config 'mixins.projectred.expansion.json'

    debug.verbose = true
    debug.export = true
}

curseforge {
    apiKey = System.getenv('CURSE_TOKEN') ?: 'XXX'

    // Expansion
    project {
        id = '229048'
        releaseType = System.getenv('CURSE_RELEASE_TYPE') ?: 'alpha'
        changelogType = 'markdown'
        changelog = rootProject.file('CHANGELOG.md')
        relations {
            requiredDependency 'project-red-core'
        }

        // Java/ForgeGradle integrations don't work after 1.18.2 port
        addGameVersion "${mc_version}"
        addGameVersion "Java ${java_lang_version}"
    }
}