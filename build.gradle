import java.time.Instant
plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
}

// Get mod version from CI, else suffix a timestamp (calculated here bc timestamp can change if done separately in each subproject)
mod_version = System.getenv("AUTO_GENERATED_VERSION") ?: "${mod_version}.${Instant.now().getEpochSecond()}"
group = 'mrtjp'
version = "${mc_version}-${mod_version}"

println "Starting build of ${name}, Version: ${mod_version}"
println "Using NeoForge: ${forge_version}, for Minecraft: ${mc_version}"

// Common submodule configurations
subprojects { p ->

    // Needs to be force-applied here to allow below configuration
    apply plugin: 'java'

    // Select Java version
    java.toolchain.languageVersion = JavaLanguageVersion.of(java_lang_version)

    // Copy properties from root
    group = rootProject.group
    mod_version = rootProject.mod_version
    version = rootProject.version

    // ForgeGradle version settings
    base {
        archivesName = rootProject.name
    }

    // Jar settings
    jar {
        archiveClassifier = p.name
    }

    // Add datagen resources to source set
    sourceSets.main.resources.srcDirs += "src/main/generated"

    // Add default repositories
    repositories {
        mavenLocal()
        maven { url = "https://maven.covers1624.net/" }
        maven { url = "https://maven.squiddev.cc" }
        maven { url = "https://maven.blamejared.com/" }
    }

    // Replace version tokens in mods.toml
    processResources {
        inputs.property 'mod_version', mod_version
        inputs.property 'mc_version', mc_version

        filesMatching('META-INF/neoforge.mods.toml') {
            expand 'file': ['jarVersion': mod_version],
                    'mc_version': mc_version,
                    'forge_version': forge_version,
                    'lang_version': forge_version.split('\\.')[0],
                    'ccl_version': ccl_version,
                    'cbm_version': cbm_version,
                    'cct_version': cct_version
        }
    }
}

// Defined explicitly so publishing can access shadowJar property
project(':fabrication') {
    apply plugin: 'com.github.johnrengelman.shadow'
}

publishing {
    repositories {
        maven {
            url "https://nexus.covers1624.net/repository/maven-releases/"
            credentials {
                username System.getenv('MAVEN_USER')
                password System.getenv('MAVEN_PASS')
            }
        }
    }
    publications {
        ProjectRed(MavenPublication) {
            artifact project(':api').jar
            artifact project(':core').jar
            artifact project(':expansion').jar
            artifact project(':exploration').jar
            artifact project(':fabrication').shadowJar
            artifact project(':illumination').jar
            artifact project(':integration').jar
            artifact project(':transmission').jar

            pom {
                name = rootProject.name
                description = rootProject.name

                url = 'https://github.com/MrTJP/ProjectRed'
                scm {
                    url = 'https://github.com/MrTJP/ProjectRed'
                    connection = 'scm:git:git://github.com/MrTJP/ProjectRed.git'
                    connection = 'scm:git:git@github.com:MrTJP/ProjectRed.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MrTJP/ProjectRed/issues'
                }
                developers {
                    developer {
                        id = 'mrtjp'
                        name = 'mrtjp'
                    }
                    developer {
                        id = 'Chicken-Bones'
                        name = 'Chicken-Bones'
                    }
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
            }
        }
    }
}
