plugins {
    id 'net.minecraftforge.gradle'
    id 'com.matthewprenger.cursegradle'
    id 'com.github.johnrengelman.shadow'
}

String mod_id = 'projectred_fabrication'

minecraft {
    mappings channel: mcp_mappings, version: mcp_mappings_version
    accessTransformer = file("../core/src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        data {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            ideaModule "${rootProject.name}.${project.name}.main"

            workingDirectory file('run')
            args '--mod', mod_id, '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods {
                '${mod_id}' { source sourceSets.main }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal")
    implementation fg.deobf("codechicken:CBMultipart:${mc_version}-${cbm_version}:universal")

    implementation project(":core")

    // Note: Fails if transitives are included. Possible ForgeGradle bug
    implementation(project(":integration")) { transitive = false }
    implementation(project(":transmission")) { transitive = false }

    // Dependencies to be packed into jar
    shadow("io.github.mrtjp:fabrication-engine:${fabrication_version}") { transitive = false }
}

// Jar settings
jar.enabled = false
shadowJar {
    // Jar configurations
    archiveClassifier = jar.archiveClassifier
    configurations = [project.configurations.shadow]

    // Attach reobf task
    finalizedBy 'reobfShadowJar'
}

build.dependsOn shadowJar

reobf {
    jar { enabled = false } // Disable unshaded jar
    shadowJar { } // Enable shadow jar reobf
}

curseforge {
    apiKey = System.getenv('CURSE_TOKEN') ?: 'XXX'

    // Fabrication
    project {
        id = '230111'
        releaseType = System.getenv('CURSE_RELEASE_TYPE') ?: 'alpha'
        changelogType = 'markdown'
        changelog = rootProject.file('CHANGELOG.md')
        relations {
            requiredDependency 'project-red-core'
            requiredDependency 'project-red-integration'
            requiredDependency 'project-red-transmission'
        }

        // Java/ForgeGradle integrations don't work after 1.18.2 port
        addGameVersion "${mc_version}"
        addGameVersion "Java ${java_lang_version}"
    }
}
