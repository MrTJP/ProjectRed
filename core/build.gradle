plugins {
    id 'net.minecraftforge.gradle'
    id 'com.matthewprenger.cursegradle'
}

String mod_id = 'projectred_core'

minecraft {
    mappings channel: mcp_mappings, version: mc_version
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        client {
            workingDirectory file('run')
            mods {
                '${mod_id}' { source sourceSets.main }
            }
        }
        server {
            workingDirectory file('run')
            mods {
                '${mod_id}' { source sourceSets.main }
            }
        }
        datagen {
            workingDirectory file('run')
            args '--mod', mod_id, '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods {
                '${mod_id}' { source sourceSets.main }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("codechicken:CodeChickenLib:${mc_version}-${ccl_version}:universal")
    implementation fg.deobf("codechicken:CBMultipart:${mc_version}-${cbm_version}:universal")

    // JEI
    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge-api:${jei_version}"))
    runtimeOnly(fg.deobf("mezz.jei:jei-${mc_version}-forge:${jei_version}"))

    // CCTweaked
    compileOnly fg.deobf("org.squiddev:cc-tweaked-${mc_version}:${cct_version}:api")
    runtimeOnly fg.deobf("org.squiddev:cc-tweaked-${mc_version}:${cct_version}")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

curseforge {
    apiKey = System.getenv('CURSE_TOKEN') ?: 'XXX'

    // Core
    project {
        id = '228702'
        releaseType = System.getenv('CURSE_RELEASE_TYPE') ?: 'alpha'
        changelogType = 'markdown'
        changelog = rootProject.file('CHANGELOG.md')
        relations {
            requiredDependency 'codechicken-lib-1-8' // CCL is required for everything
            requiredDependency 'cb-multipart' // CBMultipart is required for everything
        }

        // Java/ForgeGradle integrations don't work after 1.18.2 port
        addGameVersion "${mc_version}"
        addGameVersion "Java ${java_lang_version}"
    }
}
